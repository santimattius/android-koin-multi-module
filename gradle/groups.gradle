def Group(Closure closure) {
    closure.delegate = dependencies
    return closure
}

project.ext {

    androidCoreDependencies = Group {
        implementation 'androidx.appcompat:appcompat:1.5.1'
        implementation "androidx.core:core-ktx:$core_ktx_version"
    }

    composeDependencies = Group {
        implementation "androidx.activity:activity-compose:$activity_compose_version"
        implementation "androidx.compose.ui:ui:$compose_ui_version"
        implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
        implementation "androidx.compose.material:material:$material_compose_version"

        androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"

        debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
        debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    }

    lifecycleDependencies = Group {
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime_version"
    }

    roomDependencies = Group {
        def room_version = "2.4.3"

        implementation "androidx.room:room-ktx:$room_version"
        implementation "androidx.room:room-runtime:$room_version"
        ksp "androidx.room:room-compiler:$room_version"
        testImplementation "androidx.room:room-testing:$room_version"
    }

    koinDependencies = Group {
        def koin_version= "3.2.1"
        // Koin Core features
        implementation "io.insert-koin:koin-core:$koin_version"
        // Koin main features for Android
        implementation "io.insert-koin:koin-android:$koin_version"
        // Jetpack Compose
        implementation "io.insert-koin:koin-androidx-compose:3.2.0"

        def koin_ksp_version= "1.0.2"
        implementation "io.insert-koin:koin-annotations:$koin_ksp_version"
        ksp "io.insert-koin:koin-ksp-compiler:$koin_ksp_version"
    }

    airbnbDependencies = Group {
        implementation 'com.airbnb:deeplinkdispatch:6.1.0'
        ksp 'com.airbnb:deeplinkdispatch-processor:6.1.0'
    }

    testing = Group {
        testImplementation 'junit:junit:4.13.2'

        androidTestImplementation 'androidx.test.ext:junit:1.1.3'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    }
}